CLASS_DEPENDS += "native:cmake"

inherit make c

OE_CMAKE_CONF = "\
 -DCMAKE_FIND_ROOT_PATH=${HOST_SYSROOT}\
 -DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY=ONLY\
 -DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE=ONLY\
 -DCMAKE_C_COMPILER=${HOST_CC}\
 -DCMAKE_SYSTEM_NAME=Linux\
 -DCMAKE_INSTALL_PREFIX=$target_prefix\
 -DCMAKE_DATA_DIR=${datadir}\
 -DCMAKE_DOC_DIR=${docdir}\
 -DCMAKE_MAN_DIR=${mandir}\
"

OE_CMAKE_CONF:>native = "\
 -DCMAKE_INSTALL_RPATH=${STAGE_DIR}/native${stage_libdir}"
OE_CMAKE_CONF:>cross = "\
 -DCMAKE_INSTALL_RPATH=${STAGE_DIR}/cross${stage_libdir}\
 -DCMAKE_INSTALL_RPATH=${STAGE_DIR}/native${stage_libdir}"
OE_CMAKE_CONF:>machine = "\
 -DCMAKE_INSTALL_RPATH=${D}${machine_libdir}\
 -DCMAKE_INSTALL_RPATH=${D}${machine_base_libdir}"
OE_CMAKE_CONF:>sdk = "\
 -DCMAKE_INSTALL_RPATH=${D}${sdk_libdir}\
 -DCMAKE_INSTALL_RPATH=${D}${sdk_base_libdir}"
OE_CMAKE_CONF:>sdk-cross = "\
 -DCMAKE_INSTALL_RPATH=${STAGE_DIR}/sdk-cross${stage_libdir}\
 -DCMAKE_INSTALL_RPATH=${STAGE_DIR}/native${stage_libdir}"
OE_CMAKE_CONF:>canadian-cross = "\
 -DCMAKE_INSTALL_RPATH=${D}${sdk_libdir}\
 -DCMAKE_INSTALL_RPATH=${D}${sdk_base_libdir}"

OE_CMAKE_CONF:>cxx = " -DCMAKE_CXX_COMPILER=${HOST_CXX}"

EXTRA_OE_CMAKE_CONF ?= ""

# Out-of-source builds are necessary to be able to reliably re-run cmake
B = "${WORKDIR}/build"
do_configure[cleandirs] = "${B}"

do_configure() {
	cmake ${S} ${OE_CMAKE_CONF} ${EXTRA_OE_CMAKE_CONF}
}

# Local Variables:
# mode: python
# End:
