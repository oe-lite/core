## Common class for autoclass recipes
##
## If having a package that is autotools based, the way it is compiled and
## installed is similar to other autotools packages. If having a new recipe
## that is autotools based, just inherit autotools, and it should work.

# Autoconf sitefile handling
inherit c
inherit siteinfo
inherit libtool
inherit make

EXTRA_OECONF[emit] = "do_configure"
EXTRA_OECONF:>machine		= " cross_compiling=yes"
EXTRA_OECONF:>cross		= " cross_compiling=yes"
EXTRA_OECONF:>sdk-cross		= " cross_compiling=yes"
EXTRA_OECONF:>sdk		= " cross_compiling=yes"
EXTRA_OECONF:>canadian-cross	= " cross_compiling=yes"

#EXTRA_OECONF += "--with-libtool=`which ${LIBTOOL}`"

# Arch tuple arguments for configure
OECONF_ARCHTUPLE = "--build=${BUILD_ARCH} --host=${HOST_ARCH}"
OECONF_ARCHTUPLE:>cross			= " --target=${TARGET_ARCH}"
OECONF_ARCHTUPLE:>sdk-cross 		= " --target=${TARGET_ARCH}"
OECONF_ARCHTUPLE:>canadian-cross	= " --target=${TARGET_ARCH}"
OECONF_ARCHTUPLE[emit] = "do_configure"

CONFIGURE_SCRIPT ?= "${S}/configure"

autotools_configure () {
    if [ -x "${CONFIGURE_SCRIPT}" ] ; then
        ${CONFIGURE_SCRIPT} \
 ${OECONF_ARCHTUPLE}\
 --prefix=${prefix} --exec_prefix=${exec_prefix}\
 --bindir=${bindir} --sbindir=${sbindir}\
 --libexecdir=${libexecdir} --datadir=${datadir} --sysconfdir=${sysconfdir}\
 --sharedstatedir=${sharedstatedir} --localstatedir=${localstatedir}\
 --libdir=${libdir} --includedir=${includedir}\
 --infodir=${infodir} --mandir=${mandir}\
 ${EXTRA_OECONF} $@
    else
        oefatal "no configure script found"
    fi
}
autotools_configure[emit] = "do_configure"

do_configure () {
    do_configure_autotools
}

AUTOCONFDIRS ?= "/"
UPDATE_GNUCONF ?= "0"
UPDATE_GNUCONF:>TARGET_CPU_aarch64 = "1"
CLASS_DEPENDS += " native:autotools-config"

do_configure_autotools () {
    if [ "${UPDATE_GNUCONF}" != "0" ] ; then
	for autoconfdir in ${AUTOCONFDIRS} ; do
            rm -f ${S}$autoconfdir/config.{guess,sub}
            cp ${BUILD_SYSROOT}/config.{guess,sub} ${S}$autoconfdir
        done
    fi

    if [ -f Makefile -a "${AUTOTOOLS_DISTCLEAN}" != "0" ] ; then
        ${MAKE} distclean || true
    fi

    if [ -e "${CONFIGURE_SCRIPT}" ]; then
        autotools_configure
    else
        oenote "nothing to configure"
    fi

    libtool_script_fixup
}
AUTOTOOLS_DISTCLEAN ?= "1"

# Local Variables:
# mode: python
# End:
